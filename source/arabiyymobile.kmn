store(&Version) '17.0'
store(&TARGETS) 'mobile'
c store(&mnemoniclayout) '0'

store(&COPYRIGHT) 'Copyright 2024 © Anas - أنَس عِمران'

store(&KEYBOARDVERSION) '1.00'
store(&BITMAP) 'Arabiyy.ico'
store(&kmw_embedcss) 'Arabiyy.css'

c store(&ETHNOLOGUECODE) 'arb ara aao arq bbz abv shu acy adf avl arz afb ayh acw ayl acm ary ars apc ayp acx aec ayn ssh ajp pga apd acq abh aeb auz'
c store(&LANGUAGE) 'x0401'
c store(&WINDOWSLANGUAGES) 'x0401'
store(&KMW_RTL) '1'
store(&NAME) 'Arabiyy - عَرَبِيّ'
store(&LAYOUTFILE) 'arabiyymobile.keyman-touch-layout'
store(&VISUALKEYBOARD) 'arabiyymobile.kvks'

begin Unicode > use(main)

store(cons) 'ذ' 'ظ' 'خ' 'ح' 'ض' 'ص' 'ى' 'ط' 'م' 'ن' 'ب' 'ث' 'ش' 'ز' 'ل' 'ك' 'ج' 'ه' 'غ' 'ف' 'د' 'س' 'ع' 'ت' 'ر' 'ق'
store(vowel) 'اوي'
store(tanween) 'ً' 'ٌ' 'ٍ'
store(diacritic) 'ُ' 'َ' 'ِ' 'ّ'
store(FathaVariant) 'َ' 'أ' 'ا'
store(thing) 'ة' 'ُ' 'َ' 'ِ' 'ّ' 'ً' 'ٌ' 'ٍ' 'أ' 'ءاوي' 'ذ' 'ظ' 'خ' 'ح' 'ض' 'ص' 'ى' 'ط' 'م' 'ن' 'ب' 'ث' 'ش' 'ز' 'ل' 'ك' 'ج' 'ه' 'غ' 'ف' 'د' 'س' 'ع' 'ت' 'ر' 'ق'
c store(alif) 'أُ' 'أ' 'إ'

group(main) using keys

+ [K_SPACE] > ' '

+ [K_F10] > 'ض'
+ [SHIFT K_0] > '('

+ [K_F1] > 'ث'
+ [SHIFT K_1] > '!'

+ [K_F2] > 'ء'
+ [SHIFT K_2] > '@'

+ [K_F3] > 'ع'
+ [SHIFT K_3] > '#'

+ [K_F4] > 'ۚ'
+ [SHIFT K_4] > '$'

+ [K_F5] > 'خ'
+ [SHIFT K_5] > '%'

+ [K_F6] > 'ط'
+ [SHIFT K_6] > '^'

+ [K_F7] > 'ح'
+ [SHIFT K_7] > '&'

+ [K_F8] > 'ذ'
+ [SHIFT K_8] > '*'

+ [K_F9] > 'ظ'
+ [SHIFT K_9] > ')'

+ [K_A] > 'َ'
+ [SHIFT K_A] > 'ا'

+ [K_B] > 'ب'
+ [SHIFT K_B] > 'لآ'

+ [K_C] > 'ص'
+ [SHIFT K_C] > '}'

+ [K_D] > 'د'
+ [SHIFT K_D] > ']'

+ [K_E] > 'ْ'
+ [SHIFT K_E] > 'ُ'

+ [K_F] > 'ف'
+ [SHIFT K_F] > '['

+ [K_G] > 'غ'
+ [SHIFT K_G] > 'لأ'

+ [K_H] > 'ه'
+ [SHIFT K_H] > 'أ'

+ [K_I] > 'ِ'
+ [SHIFT K_I] > 'ي'

+ [K_J] > 'ج'
+ [SHIFT K_J] > 'ـ'

+ [K_K] > 'ك'
+ [SHIFT K_K] > '،'

+ [K_L] > 'ل'
+ [SHIFT K_L] > '/'

+ [K_M] > 'م'
+ [SHIFT K_M] > '’'

+ [K_N] > 'ن'
+ [SHIFT K_N] > 'آ'

+ [K_O] > 'ُ'
+ [SHIFT K_O] > 'و'

+ [K_P] > 'ح'
+ [SHIFT K_P] > '؛'

+ [K_Q] > 'ق'
+ [SHIFT K_Q] > 'ِ'

+ [K_R] > 'ر'
+ [SHIFT K_R] > 'ٌ'

+ [K_S] > 'س'
+ [SHIFT K_S] > 'ٍ'

+ [K_T] > 'ت'
+ [SHIFT K_T] > 'لإ'

+ [K_U] > 'ع'
+ [SHIFT K_U] > '‘'

+ [K_V] > 'ى'
+ [SHIFT K_V] > '{'

+ [K_W] > 'و'
+ [SHIFT K_W] > 'ً'

+ [K_X] > 'ش'
+ [SHIFT K_X] > 'ْ'

+ [K_Y] > 'ي'
+ [SHIFT K_Y] > 'إ'

+ [K_Z] > 'ز'
+ [SHIFT K_Z] > '~'

+ [K_COMMA] > '،'
+ [SHIFT K_COMMA] > '?'
+ [K_COLON] > '؛'
+ [SHIFT K_COLON] > ':'
+ [K_PERIOD] > '.'
+ [SHIFT K_PERIOD] > '!'

+ [K_1] > '1'
+ [K_2] > '2'
+ [K_3] > '3'
+ [K_4] > '4'
+ [K_5] > '5'
+ [K_6] > '6'
+ [K_7] > '7'
+ [K_8] > '8'
+ [K_9] > '9'
+ [K_0] > '0'
+ [K_F11] > U+200E

+ [K_EQUAL] > '='
+ [SHIFT K_EQUAL] > '+'

+ [K_HYPHEN] > '-'
+ [SHIFT K_HYPHEN] > '_'

+ [K_SLASH] > '/'
+ [K_LBRKT] > '['
+ [K_RBRKT] > ']'

match > use(post)
group(post)

c Numbers
store(digitkey) '0123456789'
store(digit) '٠١٢٣٤٥٦٧٨٩'
store(NumOpt) '0'

U+200E > nul
U+200E if(NumOpt = '0') > set(NumOpt = '1') save(NumOpt)
U+200E if(NumOpt = '1') > set(NumOpt = '0') save(NumOpt)

any(digitkey) if(NumOpt = '1') > index(digit, 1)

c Shadda
any(cons) index(cons, 1) > context(1) 'ّ'

'و' 'و' > 'وّ'
'ي' 'ي' > 'يّ'

'طتّ' 'ت' > 'طّ'
'صسّ' 'س' > 'صّ'
'ضدّ' 'د' > 'ضّ'
'خكّ' 'ك' > 'خّ'

c Islamic keyword shortcuts 💖
store(Hotkey) '/' '\'

store(FontTashkeelOpt) '0'
c if(FontTashkeelOpt = '0') > outs('الله')
c if(FontTashkeelOpt = '1') > outs('اللَّه')
U+200E if(FontTashkeelOpt = '0') > set(FontTashkeelOpt = '1') save(FontTashkeelOpt)
U+200E if(FontTashkeelOpt = '1') > set(FontTashkeelOpt = '0') save(FontTashkeelOpt)
c store(LJKeys) 'َلَّه' 'الَّه' 'اللَه'
c any(LJKeys) if(FontTashkeelOpt = '0') > 'الله'
c any(LJKeys) if(FontTashkeelOpt = '1') > 'اللَّه'
'َلَّه' > 'الله'
'الَّه' > 'الله'
'اللَه' > 'الله'

'بِسمِلَّه' > 'بِسمِ اللَّه' c bismillah
any(Hotkey) 'بِسمِلَّه' > 'بِسمِ اللَّهِ الرَّحمـٰنِ الرَّحيم' c /bismillah
any(Hotkey) any(Hotkey) 'بِسمِلَّه' > '﷽' c //bismillah
any(Hotkey) 'سوت' > 'سُبحانَهُ وَتَعالىٰ' c swt
any(Hotkey) 'توت' > 'تَبارَكَ وَتَعالىٰ' c twt
any(Hotkey) 'أزوج'> 'عَزَّ وَجَلّ' c azwj
any(Hotkey) 'َزوج'> 'عَزَّ وَجَلّ' c azwj
any(Hotkey) 'عزوج'> 'عَزَّ وَجَلّ' c 3zwj
any(Hotkey) 'جج' > 'جَلَّ جَلالُه' c /jj
any(Hotkey) any(Hotkey) 'جج' > 'ﷻ' c //jj

any(Hotkey) 'صَ' 'ُ' > 'صَلَّى اللَّٰهُ عَلَيهِ وَسَلَّم' c /Saw
any(Hotkey) 'سَ' 'ُ' > 'صَلَّى اللَّٰهُ عَلَيهِ وَسَلَّم' c /saw
any(Hotkey) any(Hotkey) 'صَ' 'ُ' > 'ﷺ' c //Saw
any(Hotkey) any(Hotkey) 'سَ' 'ُ' > 'ﷺ' c //saw

any(Hotkey) 'رَ' > 'رَضِيَ اللَّٰهُ عَنه' c /ra

store(QuranSymbols1) 'ۚ' 'ۖ' 'ۗ' 'ۙ' 'ۛ' '۞' '۝' '۩' 'ۡ' 'ۢ' 'ۣ' 'ۤ' 'ۥ' 'ۦ' 'ۧ' 'ۨ' 'ۘ' 'ۖ' 'ۜ'
store(QuranSymbols2) 'ۖ' 'ۗ' 'ۙ' 'ۛ' '۞' '۝' '۩' 'ۡ' 'ۢ' 'ۣ' 'ۤ' 'ۥ' 'ۦ' 'ۧ' 'ۨ' 'ۘ' 'ۖ' 'ۜ' 'ۚ'
any(QuranSymbols1) '/' > index(QuranSymbols2, 1)
any(QuranSymbols1) 'ۚ' > index(QuranSymbols2, 1)

c Handle diacritics + vowels
store(harf) 'ب' 'ف' 'و' 'ك' 'أ' nul ''

'َ' 'َ'  > 'ا'
'ا' 'َ' > 'أ'
'أَ' 'َ' > 'ى' 
'أَ' any(cons) > 'أ' context(3)
'أَ' any(vowel) > 'أ' context(3)
'ى' 'َ' > 'ٰ'
'ٰ' 'َ' > 'آ'

'َى' > 'ى'
'َة' ' ' > 'ة ' 
'َة' any(diacritic) ' ' > 'ة' context(3) ' '
'َة' any(tanween) ' ' > 'ة' context(3) ' '

'ُ' 'ُ'  > 'و'
'و' 'ُ'  > 'وّ'
any(cons) 'وُ'  > context(1) 'ُوّ' c always ?
any(cons) 'ّ' 'وُ'  > context(1) context(2) 'ُوّ' c like الثُّوّار c always ?
'ءُ' 'ُ' > 'ؤُ'
any(cons) 'أُ' 'ُ' > context(1) 'ؤُ'
'الأُ' 'ُ' > 'الأو'

'ِ' 'ِ'  > 'ي'
c 'يِ' 'ِ' > 'إ
'ي' 'ِ'  > 'يّ'
any(cons) 'يِ'  > context(1) 'ِيّ' c always ?
any(cons) 'ّ' 'يِ'  > context(1) context(2) 'ِيّ' c always ?

nul 'ْ' 'ْ' > 'ا'
' ْ' 'ْ' > ' ا'
'ْ' 'ْ' > 'ّ'
'ّ' 'ْ' > 'ٓ'
'ٓ' 'ْ' > 'ـ'
'ـ' 'ْ' > 'ــ'
'ألّ' 'ْ' > 'الـ'

'ء' 'ء' > 'ئ'
'أ' 'ء' > 'ئ'
'ُ' 'ء' > 'ُؤ'
'َ' 'ء' > 'َأ'
'ِ' 'ء' > 'ِئ'

'ئْ' 'َ' > 'أ'
'ئَ' 'ْ' > 'أ'
'ئْ' 'ُ' > 'أُ'
'ئُ' 'ْ' > 'أُ'
'ؤَ' 'ْ' > 'أ'
'ؤْ' 'َ' > 'أ'
'ئْ' 'ِ' > 'إ'
'ئِ' 'ْ' > 'إ'

'َ' 'ِ'  > 'َي'
'َ' 'ُ'  > 'َو'

'ا' 'ِ'  > 'اي'
'ا' 'ُ'  > 'او'

c 'ِ' 'َ'  > 'ِا'
'ِ' 'ُ'  > 'ِو'

'ُ' 'ِ'  > 'ُي'

any(cons) 'وْ' 'ْ' > context(1) 'ُوّ'
any(cons) 'يْ' 'ْ' > context(1) 'ِيّ'

any(cons) 'َأُ' 'ُ' > context(1) 'َؤو'
any(harf) 'َأُ' 'ُ' > context(1) 'َأو'

c "'" "'" > '"'

c any(diacritic) ' ' 'إ' any(cons) any(cons) > context(1) ' ' 'ا'  context(4) context(5)
c might remove this because أنَسُ ابني is probably wrong
c '' > '' c remove ء from إ when word is 

c Handle beginning of words
c cc different ways to write لا with ء 
any(FathaVariant) 'لَْ' > 'الأ' c 'alea' = 'الأ'
any(FathaVariant) 'لُْ' > 'الأُ' c 'aleo' = 'الأُ'
any(FathaVariant) 'لِْ' > 'الإ' c 'alei' = 'الإ'

c cc typing al- + letters
nul 'ل' 'ْ' any(cons) > 'لل' context(4) 'ّ'
' ' 'ل' 'ْ' any(cons) > ' لل' context(4) 'ّ'
nul 'ل' any(diacritic) 'ْ' any(cons) > 'ل' context(3) 'ل' context(5) 'ّ'
' ' 'ل' any(diacritic) 'ْ' any(cons) > ' ل' context(3) 'ل' context(5) 'ّ'
any(harf) 'ل' any(diacritic) 'ْ' any(cons) > context(1) 'ل' context(3) 'ل' context(5) 'ّ'
any(harf) 'َ' 'ل' any(diacritic) 'ْ' any(cons) > context(1) 'َ' 'ل' context(4) 'ل' context(6) 'ّ'

nul 'ْ' any(cons) > 'ال' context(3) 'ّ'
' ' 'ْ' any(cons) > ' ال' context(3) 'ّ'
any(harf) 'ْ' any(cons) > context(1) 'ال' context(3) 'ّ'
any(harf) 'َ' 'ْ' any(cons) > context(1) 'ال' context(4) 'ّ'
any(harf) 'ِ' 'ْ' any(cons) > context(1) 'ِ' 'ال' context(4) 'ّ'

' ' any(cons) 'ّ' any(diacritic) > ' ال' context(2) 'ّ' context(4)
c ' ' any(cons) index(cons, 1)  > ' ال' context(2) 'ّ' c doesnt work

'لءَ' > 'الأ' c 'l2a' = 'الأ'
'لِلءَ' > 'لِلأ' c 'lil2a' = 'لِلأ'
'لَلءَ' > 'لَلأ' c 'lal2a' 
'لءُ' > 'الأُ' c 'l2o' = 'الأُ'
'لِلءُ' > 'لِلأُ' c 'lil2o' = 'لِلأُ'
'لَلءُ' > 'لَلأُ' c 'lal2o'
'لءِ' > 'الإ' c 'l2i' = 'الإ'
'لِلءِ' > 'لِلإ' c 'lil2i' = 'لِلإ'
'لَلءِ' > 'لَلإ' c 'lal2i' 

'بِلّل' > 'بِاللّ' c 'billl' = 'بِاللّ' like in بِاللُّغَةِ
'الّل' > 'اللّ'

'بِلْ' > 'بِال' c 'bile' = 'بِال' like in بِالفأسِ
'بِْ' any(cons) > 'بِال' context(4) 'ّ' c 'bie + (cons (twice))' = 'بِال + (that cons + a shadda)' like in بِالنّاس

c ' وا' any(cons) 'ّ' any(diacritic) > ' وال' context(4) 'ّ' context(6) c better one exists ( ْ + cons)

' ' any(harf) 'َ' 'ّ' any(cons) > ' ' context(2) 'ا' context(5)
nul any(harf) 'َ' 'ّ' any(cons) > context(2) 'ا' context(5)
' ' any(harf) 'ِ' 'ّ' any(cons) > ' ' context(2) context(3) 'ا' context(5)
nul any(harf) 'ِ' 'ّ' any(cons) > context(2) context(3) 'ا' context(5)

c cc handle diacritics at the beginning/after a space
nul 'َ' > 'أ'
nul 'ُ' > 'و'
nul 'ِ' > 'ي'

' ' 'َ' > ' أ'
' ' 'ُ' > ' و'
' ' 'ِ' > ' ي'

'وَ' 'ُ' > 'وَ' 'أُ'
'وَ' 'ِ' > 'وَ' 'إ'

'إ' 'ِ' > 'إي'
'أَ' 'ِ' > 'أي'

'أ' 'ِ' > 'إ'
'إ' 'َ' > 'أ'

'أُ' 'ِ' > 'إ'
'إ' 'ُ' > 'أُ'

'ءَ' 'َ' > 'أ'
'ءَ' 'ْ' > 'آ'
'ءَ' any(thing) > 'أ' context(3)
'أ' 'ْ' > 'آ'

'ءِ' 'ِ'  > 'إ'
'ءِ' any(thing) > 'إ' context(3)

c nul any(diacritic) > nul index(alif, 1)
c ' ' any(diacritic) > ' ' index(alif, 1)

c cc 'al' + cons/vowel = ال + that cons/vowel
nul 'أل' 'ل' > 'ألّ'
' أل' 'ل' > ' ألّ'
nul 'أل' any(cons) > 'ال' context(4)
' ' 'أل' any(cons) > ' ال' context(4)
nul 'أل' any(vowel) > 'ال' context(4)
' ' 'أل' any(vowel) > ' ال' context(4)

nul 'أل' 'ُ' any(diacritic) > 'الو' context(5) c Context should be 4 after april update
' ' 'أل' 'ُ' any(diacritic) > ' الو' context(5)
nul 'أل' 'ِ' any(diacritic) > 'الي' context(5) c Context should be 4 after april update
' ' 'أل' 'ِ' any(diacritic) > ' الي' context(5)

' ' 'ل' any(cons) any(diacritic) > ' ال' context(3) context(4)
' ' 'ل' any(vowel) any(diacritic) > ' ال' context(3) context(4)

nul 'لُ' any(diacritic) > 'الو' context(4)
' ' 'لُ' any(diacritic) > ' الو' context(4)
nul 'لِ' any(diacritic) > 'الي' context(4)
' ' 'لِ' any(diacritic) > ' الي' context(4)

c ' ' 'ا' any(cons) > ' أ' context(3)
c ' ' 'ا' any(vowel) > ' أ' context(3)
store(WordEnding) ' ' '،' '؛' ':' '!' '.' '؟'
c Handle ending of words
any(cons) 'وْ' > context(1) 'وا'
any(cons) any(diacritic) 'وْ' > context(1) context(2) 'وا'
'ء' 'وْ' > context(1) 'وا'
'ؤ' 'وْ' > context(1) 'وا'

c cc التَّنوين
'َنْ' > 'ً'
'ُنْ' > 'ٌ'
'ِنْ' > 'ٍ'

'َْن' > 'ً'
'ُْن' > 'ٌ'
'ِْن' > 'ٍ'

c cc cc تَنوينُ الهَمزة ء/ءا 
' ' 'أن' 'ْ' > ' ً'
nul 'أن' 'ْ' > 'ً'
' ' 'إن' 'ْ' > ' ٍ'
nul 'إن' 'ْ' > 'ٍ'

'يأن' 'ْ' > 'يئًا'
'اأن' 'ْ' > 'اءً'
any(thing) 'أن' 'ْ' > context(1) 'ءًا'
any(thing) 'إن' 'ْ' > context(1) 'ءٍ'

c cc التّاءُ المَربوطةُ وَتَنوينُها
'ت' 'َنّ' 'ْ' > 'ةً' c tanne
'ت' 'ُنّ' 'ْ' > 'ةٌ' c tonne
'ت' 'ِنّ' 'ْ' > 'ةٍ' c tinne

'ت' 'َنْ' > 'تًا' c tane
'تًا' 'ْ' > 'ةً' c an additional e adjusts it

'ت' any(diacritic) 'ْ' > 'ة' context(2) 
'ت' any(tanween) 'ْ' > 'ة' context(2)

'ت' 'ْ' > 'ة'
'ه' 'ْ' > 'ة' 

c cc handle alif after a fatha tanween
any(cons) 'َنْ' > index(cons, 1) 'ًا'
any(cons) 'ّ' 'َنْ' > index(cons, 1) 'ّ' 'ًا'
any(cons) 'َْن' > index(cons, 1) 'ًا'
any(cons) 'ّ' 'َْن' > index(cons, 1) 'ّ' 'ًا'
'تًْ' > 'تًا'
'ئ' 'َنْ' > 'ئ' 'ًا'
'و' 'َنْ' > 'و' 'ًا'
'ي' 'َنْ' > 'ي' 'ًا'
'ءً' 'ْ' > 'ءًا'
'وَّنْ' > 'وًّا'
'يَّنْ' > 'يًّا'

c cc handle different huruf (مُختَلَف الحُروف)
c cc cc fix لا when it's not completed

c store(Start) nul ' '
c nul 'لَ' ' ' > context(2) 'لا '

nul 'لَ' any(WordEnding) > 'لا' context(4)
' لَ' any(WordEnding) > ' لا' context(4)
nul any(harf) 'لَ' any(WordEnding) > context(2) 'لا' context(5)
' ' any(harf) 'لَ' any(WordEnding) > ' ' context(2) 'لا' context(5)
nul any(harf) any(diacritic) 'لَ' any(WordEnding) > context(2) context(3) 'لا' context(6)
' ' any(harf) any(diacritic) 'لَ' any(WordEnding) > ' ' context(2) context(3) 'لا' context(6)

'إلَّ ' > 'إلّا ' c add alif
'وِلَّ' > 'وإلّا' c 'wailla'/'willa' = 'وإلّا'

c cc fix في
nul 'فِ ' > 'في '
' فِ ' > ' في '
nul 'فِّ ' > 'فَفي '
' فِّ ' > ' فَفي '
any(harf) 'فِ ' > context(1) 'في '
any(harf) any(diacritic) 'فِ ' > context(1) context(2) 'في '

c cc fix ما
nul 'مَ ' > 'ما '
' مَ ' > ' ما '
any(harf) 'مَ ' > context(1) 'ما '
any(harf) any(diacritic) 'مَ ' > context(1) context(2) 'ما '

c cc fix يا
nul 'يَ ' > 'يا '
' يَ ' > ' يا '
any(harf) 'يَ ' > context(1) 'يا '
any(harf) any(diacritic) 'يَ ' > context(1) context(2) 'يا '

c cc Relative pronouns (الأسماءُ المَوصولَه) (ي here is either y or ii)
c 'اللَتي' > 'الَّتي' c allatي 
'اللَتِ' 'ِ' > 'الَّتي' c allatي
'اللَّتِ' 'ِ' > 'الَّتي' c elatي

c 'اللَذي' > 'الَّذي' c alladhhي
'اللَذِ' 'ِ' > 'الَّذي' c alladhhي
'اللَّذِ' 'ِ' > 'الَّذي' c eladhhي

'اللَذان' > 'اللَّذان' c alladhhaan
'اللَذَين' > 'اللَّذَين' c alladhhayn
'اللَتان' > 'اللَّتان' c allataan
'اللَتَين' > 'اللَّتَين' c allatayn

c 'اللاتي' > 'اللّاتي' c allaatي
'اللاتِ' 'ِ' > 'اللّاتي' c allaatي
c 'اللاءي' > 'اللّائي' c allaa2ي
'اللاءِ' 'ِ' > 'اللّائي' c allaa2ي

c cc some demonstrative Pronouns (أسماءُ الإشارَه)
'هاؤُلاء' > 'هـٰؤُلاء' c haa2oolaa2 
'أُلاءِك' > 'أولـٰئِك' c olaa2ik

'هوَ' > 'هُوَ'

c 'اءُ' 'ُ' > 'َـٰؤُ'

c cc Punctuation
';' > '؛'
'?' > '؟' 
',' > '،'

store(Punc_rot1) '؛' ':' '؟' '!' c '،' '.'
store(Punc_rot2) '!' '؛' ':' '؟' c '،' '.'

any(Punc_rot2) '/' > index(Punc_rot1, 1)
any(Punc_rot2) '؛' > index(Punc_rot1, 1)

any(tanween) '  ' > '. ' c always ?
'ًا' '  ' > 'ا. ' c but جِدّا shouldnt be fixed
any(diacritic) '  ' > '. '
any(thing) '  ' > context(1) '. '
'. ' 'َ' > '، أ'
'. ' 'ُ' > '، و'
'. ' 'ِ' > '، ي'
'. ' 'ْ' > '، ْ'
'. ' any(thing) > '، ' context(3)

c switch to a variant of a consonant
store(1stChar) 'د' 'ت' 'س' 'ض' 'ك'
store(2ndChar) 'ذ' 'ث' 'ش' 'ظ' 'خ'
any(1stChar) 'هه' > index(2ndChar, 1)
any(1stChar) any(diacritic) 'هه' > index(2ndChar, 1) context(2)
any(2ndChar) index(1stChar, 1) 'هه' > index(2ndChar, 1) 'ّ'
'تّ' 'ت' > 'ط'
'سّ' 'س' > 'ص'
'كّ' 'ك' > 'خ'
'هّه' > 'ح'
'دّ' 'د' > 'ض'
'ضّ' 'ض' > 'ظ'

store(a_rot1) 'َ' 'ا' 'آ' 'أ'  'ٰ' 'ى' 'ع'
store(a_rot2) 'ا' 'آ' 'أ' 'ٰ' 'ى' 'ع' 'َ'

store(d_rot1) 'د' 'ذ' 'ض' 'ظ' 
store(d_rot2) 'ذ' 'ض' 'ظ' 'د'

store(h_rot1) 'ح' 'خ' 'ه'
store(h_rot2) 'خ' 'ه' 'ح'

store(s_rot1) 'س' 'ص' 'ش'
store(s_rot2) 'ص' 'ش' 'س'

store(t_rot1) 'ت' 'ة' 'ط' 'ث'
store(t_rot2) 'ة' 'ط' 'ث' 'ت'

any(a_rot1) '/' > index(a_rot2, 1)
any(d_rot1) '/' > index(d_rot2, 1)
any(h_rot1) '/' > index(h_rot2, 1)
any(s_rot1) '/' > index(s_rot2, 1)
any(t_rot1) '/' > index(t_rot2, 1)

c any(a_rot1) 'َ' > index(a_rot2, 1)
c any(o_rot1) 'ُ' > index(o_rot2, 1)
c any(i_rot1) 'ِ' > index(i_rot2, 1)

c Override diacritics when they are different and are not used in any other rule
c store(tashkeel) 'َ' 'ُ' 'ِ'
c any(tashkeel) any(tashkeel) > context(2)

c '::' > set(&mnemoniclayout='0') save(&mnemoniclayout) c I can't do this https://help.keyman.com/developer/language/reference/set#:~:text=system%20stores%20are-,supported,-%3A
c + [K_p] [K_U] > 'صَلَّى ٱللَّٰهُ عَلَيْهِۦ وَسَلَّمَ'
